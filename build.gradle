buildscript {
    repositories {
        jcenter()
        maven { url "https://jitpack.io" }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.0.0'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.3'
        classpath 'io.freefair:gradle-plugins:2.0.0-beta2'
    }
}

apply plugin: 'io.freefair.git-version'

allprojects {
    repositories {
        jcenter()
        maven { url "https://jitpack.io" }
    }
    group = 'io.freefair'
    version = rootProject.version
}

def isCi = "true".equals(System.getenv("CI"))

subprojects {
    apply plugin: 'com.android.library'

    android {
        compileSdkVersion 23
        buildToolsVersion "23.0.2"

        defaultConfig {
            minSdkVersion 1
            versionCode 1
            versionName project.version
        }

        lintOptions {
            xmlReport !isCi
            htmlReport !isCi
            textOutput 'stdout'
            textReport isCi
        }

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_7
            targetCompatibility JavaVersion.VERSION_1_7
        }
    }

    dependencies {
        testCompile 'junit:junit:4.12'
    }

    apply plugin: 'com.github.dcendents.android-maven'
    apply plugin: 'io.freefair.android-maven-jars'
}
